module mPackmol

use mGlobal
use mStruc
use mBox

implicit none

contains

  !=================================================================================================

  subroutine Run_Packmol( lpack, lmol, lcoord, seed, tol, natoms, mass, box )

    type(StrucList), intent(inout) :: lpack, lmol, lcoord
    integer,         intent(in)    :: seed, natoms(:)
    real(rb),        intent(in)    :: tol, mass(:)
    type(tBox),      intent(inout) :: box

    integer :: i, j, imol, ncomp, nmol, molcount, unit, narg
    real(rb) :: total_mass
    character(sl) :: arg(4), box_limits, cmol
    logical :: found
    integer, allocatable :: indx(:)
    character(sl), allocatable :: atom(:)
    type(Struc), pointer :: comp, mol, coord

    comp => lpack % first
    total_mass = 0.0_rb
    allocate( indx(lpack % count()) )
    nmol = 0
    ncomp = 0
    do while (associated(comp))
      ncomp = ncomp + 1
      imol = str2int(comp % id(1))
      if (all(indx(1:nmol) /= imol)) then
        nmol = nmol + 1
        indx(nmol) = imol
      end if
      call split( comp % params, narg, arg )
      select case (arg(1))
        case ("move","fix"); molcount = 1
        case ("copy","pack"); molcount = str2int(arg(2))
      end select
      total_mass = total_mass + molcount * mass(imol)
      comp => comp % next
    end do

    ! Check if all molecules have coordinates:
    allocate( atom(nmol) )
    do i = 1, nmol
      imol = indx(i)
      cmol = int2str(imol)
      mol => lmol % first
      found = .false.
      do while (.not.found)
        found = (mol % params == cmol)
        if (.not.found) mol => mol % next
      end do
      atom(i) = mol % id(1)
      if (.not.lcoord % find( mol % id )) call error( "no coordinates defined for molecule", cmol )
    end do

    ! Create coordinate files:
    do i = 1, nmol
      imol = indx(i)
      open( newunit = unit, file = "mol"//trim(int2str(imol))//".xyz", status = "replace" )
      write(unit,'(A)') trim(int2str(natoms(imol)))
      write(unit,'("# Generated by playmol")')
      call lcoord % search( [atom(i)], coord )
      do j = 1, natoms(imol)
        write(unit,'(A)') trim(coord % id(1))//" "//trim(coord % params)
        coord => coord % next
      end do
      close(unit)
    end do

    ! Define box limits:
    call box % compute_lengths( total_mass )
    box_limits = join(real2str(0.5_rb*tol*[1,1,1]))
    box_limits = trim(box_limits)//" "//join(real2str(box % length - 0.5_rb*tol))

    ! Create packmol input script:
    open( newunit = unit, file = "packmol.inp", status = "replace" )
    write(unit,'("# Generated by playmol",/)')
    write(unit,'("tolerance ",A)') trim(real2str(tol))
    write(unit,'("filetype xyz")')
    write(unit,'("output mixture.xyz")')
    comp => lpack % first
    do while (associated(comp))
      write(unit,'(/,"structure mol",A,".xyz")') trim(comp % id(1))
      call split( comp % params, narg, arg )
      select case (arg(1))
        case ("move","fix")
          write(unit,'("  number 1")')
          if (arg(1) == "fix") write(unit,'("  center")')
          write(unit,'("  fixed ",A," 0.0 0.0 0.0")') trim(join(arg(2:4)))
        case ("copy","pack")
          write(unit,'("  number ",A)') trim(arg(2))
          write(unit,'("  inside box ",A)') trim(box_limits)
      end select
      write(unit,'("end structure")')
      comp => comp % next
    end do
    close(unit)

!--> SÃ³ falta rodar o playmol e ler o xyz gerado.

  end subroutine Run_Packmol

  !=================================================================================================


end module mPackmol
