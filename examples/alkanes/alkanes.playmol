# Simulation of liquid normal alkanes with a united-atom force field
define	N		as 20	# Number of carbon atoms
define	Nmol		as 200	# Number of molecules
define	seed		as 5348	# Seed for random numbers
define	force_field	as nerd	# United-atom force field
define	compound	as C$N	# Base for file name

define	Tb		as {1351-exp(7.253-0.1281*$N^0.543)} # Fitted boiling point
define	density		as 0.44	# Density

# Force-field parameters are defined in another file:
include		$force_field.playmol

# Atom definitions:
atom		C1	CH3
for i from 2 to {$N-1}
  atom		C$i	CH2
next
atom		C$N	CH3

# Bond definitions:
for i from 1 to {$N-1}
  bond		C$i C{$i+1}
next

build
$N
C1 0 0 0
C2 C1 $L
C3 C2 C1 $L $theta
for i from 4 to $N
  C$i C{$i-1} C{$i-2} C{$i-3} $L $theta $phi
next

box		density $density

align		1 x y

write		xyz $compound.xyz

packmol		tolerance $tol seed $seed retry 0.95
packmol		copy 1 $Nmol
packmol		action execute

write		lammpstrj $compound.lammpstrj
write		lammps    $compound.lmp
write		summary

